/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.fiber_bundle.trivialization

/-!
# Fiber bundles

Given a "base" topological space `B` and a family `E : B ‚Üí Type*` for which `bundle.total_space E`
(a type synonym for `Œ£ b, E b`) carries a topological space structure, a topological fiber bundle
structure for `total_space E` with fiber `F` is a system of local homeomorphisms to `B √ó F`, each
respecting the fiber structure ("local trivializations" of `total_space E`). We define an object
`fiber_bundle F p` carrying the data of these local trivializations.

-/

variables {Œπ : Type*} {B : Type*} {F : Type*}

open topological_space filter set bundle
open_locale topological_space classical bundle

noncomputable theory

/-! ### Pretrivializations -/

section general

variables (F) {Z : Type*} [topological_space B] [topological_space F] {proj : Z ‚Üí B}

variable [topological_space Z]

variables (E : B ‚Üí Type*)

/-! ### Fiber bundles -/

variables [topological_space (total_space E)] [‚àÄ b, topological_space (E b)]

class fiber_bundle :=
(total_space_mk_inducing [] : ‚àÄ (b : B), inducing (@total_space_mk B E b))
(trivialization_atlas [] : set (trivialization F (œÄ E)))
(trivialization_at [] : B ‚Üí trivialization F (œÄ E))
(mem_base_set_trivialization_at [] : ‚àÄ b : B, b ‚àà (trivialization_at b).base_set)
(trivialization_mem_atlas [] : ‚àÄ b : B, trivialization_at b ‚àà trivialization_atlas)

export fiber_bundle

variables {F E} [fiber_bundle F E]

@[mk_iff]
class mem_trivialization_atlas (e : trivialization F (œÄ E)) : Prop :=
(out : e ‚àà trivialization_atlas F E)

namespace fiber_bundle

variables (F)
lemma map_proj_nhds (x : total_space E) :
  map (œÄ E) (ùìù x) = ùìù x.proj :=
(trivialization_at F E x.proj).map_proj_nhds $
  (trivialization_at F E x.proj).mem_source.2 $ mem_base_set_trivialization_at F E x.proj

variables (E)
/-- The projection from a topological fiber bundle to its base is continuous. -/
@[continuity] lemma continuous_proj : continuous (œÄ E) :=
continuous_iff_continuous_at.2 $ Œª x, (map_proj_nhds F x).le

/-- The projection from a topological fiber bundle to its base is an open map. -/
lemma is_open_map_proj : is_open_map (œÄ E) :=
is_open_map.of_nhds_le $ Œª x, (map_proj_nhds F x).ge

/-- The projection from a topological fiber bundle with a nonempty fiber to its base is a surjective
map. -/
lemma surjective_proj [nonempty F] : function.surjective (œÄ E) :=
Œª b, let ‚ü®p, _, hpb‚ü© :=
  (trivialization_at F E b).proj_surj_on_base_set (mem_base_set_trivialization_at F E b) in ‚ü®p, hpb‚ü©

/-- The projection from a topological fiber bundle with a nonempty fiber to its base is a quotient
map. -/
lemma quotient_map_proj [nonempty F] : quotient_map (œÄ E) :=
(is_open_map_proj F E).to_quotient_map (continuous_proj F E) (surjective_proj F E)

lemma continuous_total_space_mk (x : B) : continuous (@total_space_mk B E x) :=
(total_space_mk_inducing F E x).continuous

end fiber_bundle

/-- Core data defining a locally trivial topological bundle with fiber `F` over a topological
space `B`. Note that "bundle" is used in its mathematical sense. This is the (computer science)
bundled version, i.e., all the relevant data is contained in the following structure. A family of
local trivializations is indexed by a type `Œπ`, on open subsets `base_set i` for each `i : Œπ`.
Trivialization changes from `i` to `j` are given by continuous maps `coord_change i j` from
`base_set i ‚à© base_set j` to the set of homeomorphisms of `F`, but we express them as maps
`B ‚Üí F ‚Üí F` and require continuity on `(base_set i ‚à© base_set j) √ó F` to avoid the topology on the
space of continuous maps on `F`. -/
@[nolint has_nonempty_instance]
structure fiber_bundle_core (Œπ : Type*) (B : Type*) [topological_space B]
  (F : Type*) [topological_space F] :=
(base_set          : Œπ ‚Üí set B)
(is_open_base_set  : ‚àÄ i, is_open (base_set i))
(index_at          : B ‚Üí Œπ)
(mem_base_set_at   : ‚àÄ x, x ‚àà base_set (index_at x))
(coord_change      : Œπ ‚Üí Œπ ‚Üí B ‚Üí F ‚Üí F)
(coord_change_self : ‚àÄ i, ‚àÄ x ‚àà base_set i, ‚àÄ v, coord_change i i x v = v)
(continuous_on_coord_change : ‚àÄ i j, continuous_on (Œªp : B √ó F, coord_change i j p.1 p.2)
                                               (((base_set i) ‚à© (base_set j)) √óÀ¢ univ))
(coord_change_comp : ‚àÄ i j k, ‚àÄ x ‚àà (base_set i) ‚à© (base_set j) ‚à© (base_set k), ‚àÄ v,
  (coord_change j k x) (coord_change i j x v) = coord_change i k x v)

namespace fiber_bundle_core

variables [topological_space B] [topological_space F] (C : fiber_bundle_core Œπ B F)

include C

/-- The index set of a topological fiber bundle core, as a convenience function for dot notation -/
@[nolint unused_arguments has_nonempty_instance]
def index := Œπ

/-- The base space of a topological fiber bundle core, as a convenience function for dot notation -/
@[nolint unused_arguments, reducible]
def base := B

/-- The fiber of a topological fiber bundle core, as a convenience function for dot notation and
typeclass inference -/
@[nolint unused_arguments has_nonempty_instance]
def fiber (x : B) := F

section fiber_instances
local attribute [reducible] fiber

instance topological_space_fiber (x : B) : topological_space (C.fiber x) := by apply_instance

end fiber_instances

/-- The total space of the topological fiber bundle, as a convenience function for dot notation.
It is by definition equal to `bundle.total_space C.fiber`, a.k.a. `Œ£ x, C.fiber x` but with a
different name for typeclass inference. -/
@[nolint unused_arguments, reducible]
def total_space := bundle.total_space C.fiber

/-- The projection from the total space of a topological fiber bundle core, on its base. -/
@[reducible, simp, mfld_simps] def proj : C.total_space ‚Üí B := bundle.total_space.proj

/-- Local homeomorphism version of the trivialization change. -/
def triv_change (i j : Œπ) : local_homeomorph (B √ó F) (B √ó F) :=
{ source      := (C.base_set i ‚à© C.base_set j) √óÀ¢ univ,
  target      := (C.base_set i ‚à© C.base_set j) √óÀ¢ univ,
  to_fun      := Œªp, ‚ü®p.1, C.coord_change i j p.1 p.2‚ü©,
  inv_fun     := Œªp, ‚ü®p.1, C.coord_change j i p.1 p.2‚ü©,
  map_source' := Œªp hp, by simpa using hp,
  map_target' := Œªp hp, by simpa using hp,
  left_inv'   := begin
    rintros ‚ü®x, v‚ü© hx,
    simp only [prod_mk_mem_set_prod_eq, mem_inter_iff, and_true, mem_univ] at hx,
    rw [C.coord_change_comp, C.coord_change_self],
    { exact hx.1 },
    { simp [hx] }
  end,
  right_inv'  := begin
    rintros ‚ü®x, v‚ü© hx,
    simp only [prod_mk_mem_set_prod_eq, mem_inter_iff, and_true, mem_univ] at hx,
    rw [C.coord_change_comp, C.coord_change_self],
    { exact hx.2 },
    { simp [hx] },
  end,
  open_source :=
    (is_open.inter (C.is_open_base_set i) (C.is_open_base_set j)).prod is_open_univ,
  open_target :=
    (is_open.inter (C.is_open_base_set i) (C.is_open_base_set j)).prod is_open_univ,
  continuous_to_fun  :=
    continuous_on.prod continuous_fst.continuous_on (C.continuous_on_coord_change i j),
  continuous_inv_fun := by simpa [inter_comm]
    using continuous_on.prod continuous_fst.continuous_on (C.continuous_on_coord_change j i) }

@[simp, mfld_simps] lemma mem_triv_change_source (i j : Œπ) (p : B √ó F) :
  p ‚àà (C.triv_change i j).source ‚Üî p.1 ‚àà C.base_set i ‚à© C.base_set j :=
by { erw [mem_prod], simp }

/-- Associate to a trivialization index `i : Œπ` the corresponding trivialization, i.e., a bijection
between `proj ‚Åª¬π (base_set i)` and `base_set i √ó F`. As the fiber above `x` is `F` but read in the
chart with index `index_at x`, the trivialization in the fiber above x is by definition the
coordinate change from i to `index_at x`, so it depends on `x`.
The local trivialization will ultimately be a local homeomorphism. For now, we only introduce the
local equiv version, denoted with a prime. In further developments, avoid this auxiliary version,
and use `C.local_triv` instead.
-/
def local_triv_as_local_equiv (i : Œπ) : local_equiv C.total_space (B √ó F) :=
{ source      := C.proj ‚Åª¬π' (C.base_set i),
  target      := C.base_set i √óÀ¢ univ,
  inv_fun     := Œªp, ‚ü®p.1, C.coord_change i (C.index_at p.1) p.1 p.2‚ü©,
  to_fun      := Œªp, ‚ü®p.1, C.coord_change (C.index_at p.1) i p.1 p.2‚ü©,
  map_source' := Œªp hp,
    by simpa only [set.mem_preimage, and_true, set.mem_univ, set.prod_mk_mem_set_prod_eq] using hp,
  map_target' := Œªp hp,
    by simpa only [set.mem_preimage, and_true, set.mem_univ, set.mem_prod] using hp,
  left_inv'   := begin
    rintros ‚ü®x, v‚ü© hx,
    change x ‚àà C.base_set i at hx,
    dsimp only,
    rw [C.coord_change_comp, C.coord_change_self],
    { exact C.mem_base_set_at _ },
    { simp only [hx, mem_inter_iff, and_self, mem_base_set_at] }
  end,
  right_inv' := begin
    rintros ‚ü®x, v‚ü© hx,
    simp only [prod_mk_mem_set_prod_eq, and_true, mem_univ] at hx,
    rw [C.coord_change_comp, C.coord_change_self],
    { exact hx },
    { simp only [hx, mem_inter_iff, and_self, mem_base_set_at] }
  end }

variable (i : Œπ)

lemma mem_local_triv_as_local_equiv_source (p : C.total_space) :
  p ‚àà (C.local_triv_as_local_equiv i).source ‚Üî p.1 ‚àà C.base_set i :=
iff.rfl

lemma mem_local_triv_as_local_equiv_target (p : B √ó F) :
  p ‚àà (C.local_triv_as_local_equiv i).target ‚Üî p.1 ‚àà C.base_set i :=
by { erw [mem_prod], simp only [and_true, mem_univ] }

lemma local_triv_as_local_equiv_apply (p : C.total_space) :
  (C.local_triv_as_local_equiv i) p = ‚ü®p.1, C.coord_change (C.index_at p.1) i p.1 p.2‚ü© := rfl

/-- The composition of two local trivializations is the trivialization change C.triv_change i j. -/
lemma local_triv_as_local_equiv_trans (i j : Œπ) :
  (C.local_triv_as_local_equiv i).symm.trans
    (C.local_triv_as_local_equiv j) ‚âà (C.triv_change i j).to_local_equiv :=
begin
  split,
  { ext x, simp only [mem_local_triv_as_local_equiv_target] with mfld_simps, refl, },
  { rintros ‚ü®x, v‚ü© hx,
    simp only [triv_change, local_triv_as_local_equiv, local_equiv.symm, true_and, prod.mk.inj_iff,
      prod_mk_mem_set_prod_eq, local_equiv.trans_source, mem_inter_iff, and_true, mem_preimage,
      proj, mem_univ, local_equiv.coe_mk, eq_self_iff_true, local_equiv.coe_trans,
      total_space.proj] at hx ‚ä¢,
    simp only [C.coord_change_comp, hx, mem_inter_iff, and_self, mem_base_set_at], }
end

/-- Topological structure on the total space of a topological bundle created from core, designed so
that all the local trivialization are continuous. -/
instance to_topological_space : topological_space (bundle.total_space C.fiber) :=
topological_space.generate_from $ ‚ãÉ (i : Œπ) (s : set (B √ó F)) (s_open : is_open s),
  {(C.local_triv_as_local_equiv i).source ‚à© (C.local_triv_as_local_equiv i) ‚Åª¬π' s}

lemma open_source' (i : Œπ) : is_open (C.local_triv_as_local_equiv i).source :=
begin
  apply topological_space.generate_open.basic,
  simp only [exists_prop, mem_Union, mem_singleton_iff],
  refine ‚ü®i, C.base_set i √óÀ¢ univ, (C.is_open_base_set i).prod is_open_univ, _‚ü©,
  ext p,
  simp only [local_triv_as_local_equiv_apply, prod_mk_mem_set_prod_eq, mem_inter_iff, and_self,
    mem_local_triv_as_local_equiv_source, and_true, mem_univ, mem_preimage],
end

open fiber_bundle

/-- Extended version of the local trivialization of a fiber bundle constructed from core,
registering additionally in its type that it is a local bundle trivialization. -/
def local_triv (i : Œπ) : trivialization F C.proj :=
{ base_set      := C.base_set i,
  open_base_set := C.is_open_base_set i,
  source_eq     := rfl,
  target_eq     := rfl,
  proj_to_fun   := Œª p hp, by { simp only with mfld_simps, refl },
  open_source := C.open_source' i,
  open_target := (C.is_open_base_set i).prod is_open_univ,
  continuous_to_fun := begin
    rw continuous_on_open_iff (C.open_source' i),
    assume s s_open,
    apply topological_space.generate_open.basic,
    simp only [exists_prop, mem_Union, mem_singleton_iff],
    exact ‚ü®i, s, s_open, rfl‚ü©
  end,
  continuous_inv_fun := begin
    apply continuous_on_open_of_generate_from ((C.is_open_base_set i).prod is_open_univ),
    assume t ht,
    simp only [exists_prop, mem_Union, mem_singleton_iff] at ht,
    obtain ‚ü®j, s, s_open, ts‚ü© : ‚àÉ j s, is_open s ‚àß t =
      (C.local_triv_as_local_equiv j).source ‚à© (C.local_triv_as_local_equiv j) ‚Åª¬π' s := ht,
    rw ts,
    simp only [local_equiv.right_inv, preimage_inter, local_equiv.left_inv],
    let e := C.local_triv_as_local_equiv i,
    let e' := C.local_triv_as_local_equiv j,
    let f := e.symm.trans e',
    have : is_open (f.source ‚à© f ‚Åª¬π' s),
    { rw [(C.local_triv_as_local_equiv_trans i j).source_inter_preimage_eq],
      exact (continuous_on_open_iff (C.triv_change i j).open_source).1
        ((C.triv_change i j).continuous_on) _ s_open },
    convert this using 1,
    dsimp [local_equiv.trans_source],
    rw [‚Üê preimage_comp, inter_assoc],
    refl,
  end,
  to_local_equiv := C.local_triv_as_local_equiv i }

/-- Preferred local trivialization of a fiber bundle constructed from core, at a given point, as
a bundle trivialization -/
def local_triv_at (b : B) : trivialization F C.proj :=
C.local_triv (C.index_at b)

@[simp, mfld_simps] lemma local_triv_at_def (b : B) :
  C.local_triv (C.index_at b) = C.local_triv_at b := rfl

/-- If an element of `F` is invariant under all coordinate changes, then one can define a
corresponding section of the fiber bundle, which is continuous. This applies in particular to the
zero section of a vector bundle. Another example (not yet defined) would be the identity
section of the endomorphism bundle of a vector bundle. -/
lemma continuous_const_section (v : F)
  (h : ‚àÄ i j, ‚àÄ x ‚àà (C.base_set i) ‚à© (C.base_set j), C.coord_change i j x v = v) :
  continuous (show B ‚Üí C.total_space, from Œª x, ‚ü®x, v‚ü©) :=
begin
  apply continuous_iff_continuous_at.2 (Œª x, _),
  have A : C.base_set (C.index_at x) ‚àà ùìù x :=
    is_open.mem_nhds (C.is_open_base_set (C.index_at x)) (C.mem_base_set_at x),
  apply ((C.local_triv_at x).to_local_homeomorph.continuous_at_iff_continuous_at_comp_left _).2,
  { simp only [(‚àò)] with mfld_simps,
    apply continuous_at_id.prod,
    have : continuous_on (Œª (y : B), v) (C.base_set (C.index_at x)) := continuous_on_const,
    apply (this.congr _).continuous_at A,
    assume y hy,
    simp only [h, hy, mem_base_set_at] with mfld_simps },
  { exact A }
end

@[simp, mfld_simps] lemma local_triv_as_local_equiv_coe :
  ‚áë(C.local_triv_as_local_equiv i) = C.local_triv i := rfl

@[simp, mfld_simps] lemma local_triv_as_local_equiv_source :
  (C.local_triv_as_local_equiv i).source = (C.local_triv i).source := rfl

@[simp, mfld_simps] lemma local_triv_as_local_equiv_target :
  (C.local_triv_as_local_equiv i).target = (C.local_triv i).target := rfl

@[simp, mfld_simps] lemma local_triv_as_local_equiv_symm :
  (C.local_triv_as_local_equiv i).symm = (C.local_triv i).to_local_equiv.symm := rfl

@[simp, mfld_simps] lemma base_set_at : C.base_set i = (C.local_triv i).base_set := rfl

@[simp, mfld_simps] lemma local_triv_apply (p : C.total_space) :
  (C.local_triv i) p = ‚ü®p.1, C.coord_change (C.index_at p.1) i p.1 p.2‚ü© := rfl

@[simp, mfld_simps] lemma local_triv_at_apply (p : C.total_space) :
  ((C.local_triv_at p.1) p) = ‚ü®p.1, p.2‚ü© :=
by { rw [local_triv_at, local_triv_apply, coord_change_self], exact C.mem_base_set_at p.1 }

@[simp, mfld_simps] lemma local_triv_at_apply_mk (b : B) (a : F) :
  ((C.local_triv_at b) ‚ü®b, a‚ü©) = ‚ü®b, a‚ü© :=
C.local_triv_at_apply _

@[simp, mfld_simps] lemma mem_local_triv_source (p : C.total_space) :
  p ‚àà (C.local_triv i).source ‚Üî p.1 ‚àà (C.local_triv i).base_set := iff.rfl

@[simp, mfld_simps] lemma mem_local_triv_at_source (p : C.total_space) (b : B) :
  p ‚àà (C.local_triv_at b).source ‚Üî p.1 ‚àà (C.local_triv_at b).base_set := iff.rfl

@[simp, mfld_simps] lemma mem_local_triv_target (p : B √ó F) :
  p ‚àà (C.local_triv i).target ‚Üî p.1 ‚àà (C.local_triv i).base_set :=
trivialization.mem_target _

@[simp, mfld_simps] lemma mem_local_triv_at_target (p : B √ó F) (b : B) :
  p ‚àà (C.local_triv_at b).target ‚Üî p.1 ‚àà (C.local_triv_at b).base_set :=
trivialization.mem_target _

@[simp, mfld_simps] lemma local_triv_symm_apply (p : B √ó F) :
  (C.local_triv i).to_local_homeomorph.symm p =
    ‚ü®p.1, C.coord_change i (C.index_at p.1) p.1 p.2‚ü© := rfl

@[simp, mfld_simps] lemma mem_local_triv_at_base_set (b : B) :
  b ‚àà (C.local_triv_at b).base_set :=
by { rw [local_triv_at, ‚Üêbase_set_at], exact C.mem_base_set_at b, }

/-- The inclusion of a fiber into the total space is a continuous map. -/
@[continuity]
lemma continuous_total_space_mk (b : B) :
  continuous (total_space_mk b : C.fiber b ‚Üí bundle.total_space C.fiber) :=
begin
  rw [continuous_iff_le_induced, fiber_bundle_core.to_topological_space],
  apply le_induced_generate_from,
  simp only [total_space_mk, mem_Union, mem_singleton_iff, local_triv_as_local_equiv_source,
    local_triv_as_local_equiv_coe],
  rintros s ‚ü®i, t, ht, rfl‚ü©,
  rw [‚Üê((C.local_triv i).source_inter_preimage_target_inter t), preimage_inter, ‚Üêpreimage_comp,
    trivialization.source_eq],
  apply is_open.inter,
  { simp only [total_space.proj, proj, ‚Üêpreimage_comp],
    by_cases (b ‚àà (C.local_triv i).base_set),
    { rw preimage_const_of_mem h, exact is_open_univ, },
    { rw preimage_const_of_not_mem h, exact is_open_empty, }},
  { simp only [function.comp, local_triv_apply],
    rw [preimage_inter, preimage_comp],
    by_cases (b ‚àà C.base_set i),
    { have hc : continuous (Œª (x : C.fiber b), (C.coord_change (C.index_at b) i b) x),
        from (C.continuous_on_coord_change (C.index_at b) i).comp_continuous
          (continuous_const.prod_mk continuous_id) (Œª x, ‚ü®‚ü®C.mem_base_set_at b, h‚ü©, mem_univ x‚ü©),
      exact (((C.local_triv i).open_target.inter ht).preimage (continuous.prod.mk b)).preimage hc },
    { rw [(C.local_triv i).target_eq, ‚Üêbase_set_at, mk_preimage_prod_right_eq_empty h,
        preimage_empty, empty_inter],
      exact is_open_empty, }}
end

/-- A topological fiber bundle constructed from core is indeed a topological fiber bundle. -/
instance fiber_bundle : fiber_bundle F C.fiber :=
{ total_space_mk_inducing := Œª b, ‚ü® begin refine le_antisymm _ (Œª s h, _),
    { rw ‚Üêcontinuous_iff_le_induced,
      exact continuous_total_space_mk C b, },
    { refine is_open_induced_iff.mpr ‚ü®(C.local_triv_at b).source ‚à© (C.local_triv_at b) ‚Åª¬π'
        ((C.local_triv_at b).base_set √óÀ¢ s), (continuous_on_open_iff
        (C.local_triv_at b).open_source).mp (C.local_triv_at b).continuous_to_fun _
        ((C.local_triv_at b).open_base_set.prod h), _‚ü©,
      rw [preimage_inter, ‚Üêpreimage_comp, function.comp],
      simp only [total_space_mk],
      refine ext_iff.mpr (Œª a, ‚ü®Œª ha, _, Œª ha, ‚ü®C.mem_base_set_at b, _‚ü©‚ü©),
      { simp only [mem_prod, mem_preimage, mem_inter_iff, local_triv_at_apply_mk] at ha,
        exact ha.2.2, },
      { simp only [mem_prod, mem_preimage, mem_inter_iff, local_triv_at_apply_mk],
        exact ‚ü®C.mem_base_set_at b, ha‚ü©, } } end‚ü©,
  trivialization_atlas := range C.local_triv,
  trivialization_at := Œª x, C.local_triv (C.index_at x),
  mem_base_set_trivialization_at := C.mem_base_set_at,
  trivialization_mem_atlas := Œª x, mem_range_self _ }

/-- The projection on the base of a topological bundle created from core is continuous -/
lemma continuous_proj : continuous C.proj :=
by { haveI := C.fiber_bundle, exact continuous_proj F C.fiber }

/-- The projection on the base of a topological bundle created from core is an open map -/
lemma is_open_map_proj : is_open_map C.proj :=
by { haveI := C.fiber_bundle, exact is_open_map_proj F C.fiber }

end fiber_bundle_core

end general

namespace bundle
instance [I : topological_space F] : ‚àÄ x : B, topological_space (trivial B F x) := Œª x, I

instance [t‚ÇÅ : topological_space B] [t‚ÇÇ : topological_space F] :
  topological_space (total_space (trivial B F)) :=
induced total_space.proj t‚ÇÅ ‚äì induced (trivial.proj_snd B F) t‚ÇÇ

/-! ### The trivial fiber bundle -/
namespace trivial

variables (B F) [topological_space B] [topological_space F]

/-- Local trivialization for trivial bundle. -/
def trivialization : trivialization F (œÄ (bundle.trivial B F)) :=
{ to_fun := Œª x, (x.fst, x.snd),
  inv_fun := Œª y, ‚ü®y.fst, y.snd‚ü©,
  source := univ,
  target := univ,
  map_source' := Œª x h, mem_univ (x.fst, x.snd),
  map_target' := Œª y h,  mem_univ ‚ü®y.fst, y.snd‚ü©,
  left_inv' := Œª x h, sigma.eq rfl rfl,
  right_inv' := Œª x h, prod.ext rfl rfl,
  open_source := is_open_univ,
  open_target := is_open_univ,
  continuous_to_fun := by { rw [‚Üêcontinuous_iff_continuous_on_univ, continuous_iff_le_induced],
    simp only [prod.topological_space, induced_inf, induced_compose], exact le_rfl, },
  continuous_inv_fun := by { rw [‚Üêcontinuous_iff_continuous_on_univ, continuous_iff_le_induced],
    simp only [bundle.total_space.topological_space, induced_inf, induced_compose],
    exact le_rfl, },
  base_set := univ,
  open_base_set := is_open_univ,
  source_eq := rfl,
  target_eq := by simp only [univ_prod_univ],
  proj_to_fun := Œª y hy, rfl }

@[simp]
lemma trivialization_source : (trivialization B F).source = univ := rfl

@[simp]
lemma trivialization_target : (trivialization B F).target = univ := rfl

instance : fiber_bundle F (bundle.trivial B F) :=
{ trivialization_atlas := {trivialization B F},
  trivialization_at := Œª x, trivialization B F,
  mem_base_set_trivialization_at := mem_univ,
  trivialization_mem_atlas := Œª x, mem_singleton _,
  total_space_mk_inducing := Œª b, ‚ü®begin
    have : (Œª (x : trivial B F b), x) = @id F, by { ext x, refl },
    simp only [total_space.topological_space, induced_inf, induced_compose, function.comp,
      total_space.proj, induced_const, top_inf_eq, trivial.proj_snd, id.def,
      trivial.topological_space, this, induced_id],
  end‚ü© }

-- instance : mem_trivialization_atlas (trivialization B F) := ‚ü®mem_singleton _‚ü©
variables {B F}
lemma eq_trivialization (e : _root_.trivialization F (œÄ (bundle.trivial B F)))
  [he : mem_trivialization_atlas e] : e = trivialization B F :=
mem_singleton_iff.mp he.1

end trivial

end bundle

/-! ### The fibrewise product of two fibre bundles -/

open trivialization
namespace bundle

variables (E‚ÇÅ : B ‚Üí Type*) (E‚ÇÇ : B ‚Üí Type*)
variables [topological_space (total_space E‚ÇÅ)] [topological_space (total_space E‚ÇÇ)]

/-- Equip the total space of the fibrewise product of two topological fiber bundles `E‚ÇÅ`, `E‚ÇÇ` with
the induced topology from the diagonal embedding into `total_space E‚ÇÅ √ó total_space E‚ÇÇ`. -/
instance prod.topological_space :
  topological_space (total_space (E‚ÇÅ √ó·µá E‚ÇÇ)) :=
topological_space.induced
  (Œª p, ((‚ü®p.1, p.2.1‚ü© : total_space E‚ÇÅ), (‚ü®p.1, p.2.2‚ü© : total_space E‚ÇÇ)))
  (by apply_instance : topological_space (total_space E‚ÇÅ √ó total_space E‚ÇÇ))

/-- The diagonal map from the total space of the fibrewise product of two topological fiber bundles
`E‚ÇÅ`, `E‚ÇÇ` into `total_space E‚ÇÅ √ó total_space E‚ÇÇ` is `inducing`. -/
lemma prod.inducing_diag : inducing
  (Œª p, (‚ü®p.1, p.2.1‚ü©, ‚ü®p.1, p.2.2‚ü©) :
    total_space (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí total_space E‚ÇÅ √ó total_space E‚ÇÇ) :=
‚ü®rfl‚ü©

end bundle

open bundle

variables [topological_space B]

variables (F‚ÇÅ : Type*) [topological_space F‚ÇÅ]
  (E‚ÇÅ : B ‚Üí Type*) [topological_space (total_space E‚ÇÅ)]

variables (F‚ÇÇ : Type*) [topological_space F‚ÇÇ]
  (E‚ÇÇ : B ‚Üí Type*) [topological_space (total_space E‚ÇÇ)]

namespace trivialization
variables (e‚ÇÅ : trivialization F‚ÇÅ (œÄ E‚ÇÅ))
variables (e‚ÇÇ : trivialization F‚ÇÇ (œÄ E‚ÇÇ))

include e‚ÇÅ e‚ÇÇ
variables {F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ}

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the forward
function for the construction `trivialization.prod`, the induced
trivialization for the direct sum of `E‚ÇÅ` and `E‚ÇÇ`. -/
def prod.to_fun' : total_space (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí B √ó (F‚ÇÅ √ó F‚ÇÇ) :=
Œª p, ‚ü®p.1, (e‚ÇÅ ‚ü®p.1, p.2.1‚ü©).2, (e‚ÇÇ ‚ü®p.1, p.2.2‚ü©).2‚ü©

variables {e‚ÇÅ e‚ÇÇ}

lemma prod.continuous_to_fun : continuous_on (prod.to_fun' e‚ÇÅ e‚ÇÇ)
  (œÄ (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Åª¬π' (e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set)) :=
begin
  let f‚ÇÅ : total_space (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí total_space E‚ÇÅ √ó total_space E‚ÇÇ :=
    Œª p, ((‚ü®p.1, p.2.1‚ü© : total_space E‚ÇÅ), (‚ü®p.1, p.2.2‚ü© : total_space E‚ÇÇ)),
  let f‚ÇÇ : total_space E‚ÇÅ √ó total_space E‚ÇÇ ‚Üí (B √ó F‚ÇÅ) √ó (B √ó F‚ÇÇ) := Œª p, ‚ü®e‚ÇÅ p.1, e‚ÇÇ p.2‚ü©,
  let f‚ÇÉ : (B √ó F‚ÇÅ) √ó (B √ó F‚ÇÇ) ‚Üí B √ó F‚ÇÅ √ó F‚ÇÇ := Œª p, ‚ü®p.1.1, p.1.2, p.2.2‚ü©,
  have hf‚ÇÅ : continuous f‚ÇÅ := (prod.inducing_diag E‚ÇÅ E‚ÇÇ).continuous,
  have hf‚ÇÇ : continuous_on f‚ÇÇ (e‚ÇÅ.source √óÀ¢ e‚ÇÇ.source) :=
    e‚ÇÅ.to_local_homeomorph.continuous_on.prod_map e‚ÇÇ.to_local_homeomorph.continuous_on,
  have hf‚ÇÉ : continuous f‚ÇÉ :=
    (continuous_fst.comp continuous_fst).prod_mk (continuous_snd.prod_map continuous_snd),
  refine ((hf‚ÇÉ.comp_continuous_on hf‚ÇÇ).comp hf‚ÇÅ.continuous_on _).congr _,
  { rw [e‚ÇÅ.source_eq, e‚ÇÇ.source_eq],
    exact maps_to_preimage _ _ },
  rintros ‚ü®b, v‚ÇÅ, v‚ÇÇ‚ü© ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©,
  simp only [prod.to_fun', prod.mk.inj_iff, eq_self_iff_true, and_true],
  rw e‚ÇÅ.coe_fst,
  rw [e‚ÇÅ.source_eq, mem_preimage],
  exact hb‚ÇÅ,
end

variables (e‚ÇÅ e‚ÇÇ) [‚àÄ b, has_zero (E‚ÇÅ b)] [‚àÄ b, has_zero (E‚ÇÇ b)]

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the inverse
function for the construction `trivialization.prod`, the induced
trivialization for the direct sum of `E‚ÇÅ` and `E‚ÇÇ`. -/
def prod.inv_fun' (p : B √ó (F‚ÇÅ √ó F‚ÇÇ)) : total_space (E‚ÇÅ √ó·µá E‚ÇÇ) :=
‚ü®p.1, e‚ÇÅ.symm p.1 p.2.1, e‚ÇÇ.symm p.1 p.2.2‚ü©

variables {e‚ÇÅ e‚ÇÇ}

lemma prod.left_inv {x : total_space (E‚ÇÅ √ó·µá E‚ÇÇ)}
  (h : x ‚àà œÄ (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Åª¬π' (e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set)) :
  prod.inv_fun' e‚ÇÅ e‚ÇÇ (prod.to_fun' e‚ÇÅ e‚ÇÇ x) = x :=
begin
  obtain ‚ü®x, v‚ÇÅ, v‚ÇÇ‚ü© := x,
  obtain ‚ü®h‚ÇÅ : x ‚àà e‚ÇÅ.base_set, h‚ÇÇ : x ‚àà e‚ÇÇ.base_set‚ü© := h,
  simp only [prod.to_fun', prod.inv_fun', symm_apply_apply_mk, h‚ÇÅ, h‚ÇÇ]
end

lemma prod.right_inv {x : B √ó F‚ÇÅ √ó F‚ÇÇ}
  (h : x ‚àà (e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set) √óÀ¢ (univ : set (F‚ÇÅ √ó F‚ÇÇ))) :
  prod.to_fun' e‚ÇÅ e‚ÇÇ (prod.inv_fun' e‚ÇÅ e‚ÇÇ x) = x :=
begin
  obtain ‚ü®x, w‚ÇÅ, w‚ÇÇ‚ü© := x,
  obtain ‚ü®‚ü®h‚ÇÅ : x ‚àà e‚ÇÅ.base_set, h‚ÇÇ : x ‚àà e‚ÇÇ.base_set‚ü©, -‚ü© := h,
  simp only [prod.to_fun', prod.inv_fun', apply_mk_symm, h‚ÇÅ, h‚ÇÇ]
end

lemma prod.continuous_inv_fun :
  continuous_on (prod.inv_fun' e‚ÇÅ e‚ÇÇ) ((e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set) √óÀ¢ univ) :=
begin
  rw (prod.inducing_diag E‚ÇÅ E‚ÇÇ).continuous_on_iff,
  have H‚ÇÅ : continuous (Œª p : B √ó F‚ÇÅ √ó F‚ÇÇ, ((p.1, p.2.1), (p.1, p.2.2))) :=
    (continuous_id.prod_map continuous_fst).prod_mk (continuous_id.prod_map continuous_snd),
  refine (e‚ÇÅ.continuous_on_symm.prod_map e‚ÇÇ.continuous_on_symm).comp H‚ÇÅ.continuous_on _,
  exact Œª x h, ‚ü®‚ü®h.1.1, mem_univ _‚ü©, ‚ü®h.1.2, mem_univ _‚ü©‚ü©
end

variables (e‚ÇÅ e‚ÇÇ)
variables [Œ† x : B, topological_space (E‚ÇÅ x)] [Œ† x : B, topological_space (E‚ÇÇ x)]
  [fiber_bundle F‚ÇÅ E‚ÇÅ] [fiber_bundle F‚ÇÇ E‚ÇÇ]

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the induced
trivialization for the direct sum of `E‚ÇÅ` and `E‚ÇÇ`, whose base set is `e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set`.

Either one of `[‚àÄ b, has_zero (E‚ÇÅ b)] [‚àÄ b, has_zero (E‚ÇÇ b)]` would suffice for this, as would either
one of `[fiber_bundle F‚ÇÅ E‚ÇÅ] [fiber_bundle F‚ÇÇ E‚ÇÇ]`.  We `nolint unused_arguments` to require both for
symmetry.
-/
-- @[nolint unused_arguments]
def prod : trivialization (F‚ÇÅ √ó F‚ÇÇ) (œÄ (E‚ÇÅ √ó·µá E‚ÇÇ)) :=
{ to_fun := prod.to_fun' e‚ÇÅ e‚ÇÇ,
  inv_fun := prod.inv_fun' e‚ÇÅ e‚ÇÇ,
  source := (œÄ (E‚ÇÅ √ó·µá E‚ÇÇ)) ‚Åª¬π' (e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set),
  target := (e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set) √óÀ¢ set.univ,
  map_source' := Œª x h, ‚ü®h, set.mem_univ _‚ü©,
  map_target' := Œª x h, h.1,
  left_inv' := Œª x, prod.left_inv,
  right_inv' := Œª x, prod.right_inv,
  open_source := begin
    refine (e‚ÇÅ.open_base_set.inter e‚ÇÇ.open_base_set).preimage _,
    exact (continuous_proj F‚ÇÅ E‚ÇÅ).comp (prod.inducing_diag E‚ÇÅ E‚ÇÇ).continuous.fst,
  end,
  open_target := (e‚ÇÅ.open_base_set.inter e‚ÇÇ.open_base_set).prod is_open_univ,
  continuous_to_fun := prod.continuous_to_fun,
  continuous_inv_fun := prod.continuous_inv_fun,
  base_set := e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set,
  open_base_set := e‚ÇÅ.open_base_set.inter e‚ÇÇ.open_base_set,
  source_eq := rfl,
  target_eq := rfl,
  proj_to_fun := Œª x h, rfl }

@[simp] lemma base_set_prod : (prod e‚ÇÅ e‚ÇÇ).base_set = e‚ÇÅ.base_set ‚à© e‚ÇÇ.base_set :=
rfl

variables {e‚ÇÅ e‚ÇÇ}

lemma prod_symm_apply (x : B) (w‚ÇÅ : F‚ÇÅ) (w‚ÇÇ : F‚ÇÇ) : (prod e‚ÇÅ e‚ÇÇ).to_local_equiv.symm (x, w‚ÇÅ, w‚ÇÇ)
  = ‚ü®x, e‚ÇÅ.symm x w‚ÇÅ, e‚ÇÇ.symm x w‚ÇÇ‚ü© :=
rfl

end trivialization

open trivialization

variables [Œ† x : B, topological_space (E‚ÇÅ x)] [Œ† x : B, topological_space (E‚ÇÇ x)]
  [fiber_bundle F‚ÇÅ E‚ÇÅ] [fiber_bundle F‚ÇÇ E‚ÇÇ] [Œ† b, has_zero (E‚ÇÅ b)] [Œ† b, has_zero (E‚ÇÇ b)]

/-- The product of two fiber bundles is a fiber bundle. -/
instance _root_.bundle.prod.fiber_bundle : fiber_bundle (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) :=
{ total_space_mk_inducing := Œª b,
  begin
    rw (prod.inducing_diag E‚ÇÅ E‚ÇÇ).inducing_iff,
    exact (total_space_mk_inducing F‚ÇÅ E‚ÇÅ b).prod_mk (total_space_mk_inducing F‚ÇÇ E‚ÇÇ b),
  end,
  trivialization_atlas := (Œª (p : trivialization F‚ÇÅ (œÄ E‚ÇÅ) √ó trivialization F‚ÇÇ (œÄ E‚ÇÇ)), p.1.prod p.2) ''
    (trivialization_atlas F‚ÇÅ E‚ÇÅ √óÀ¢ trivialization_atlas F‚ÇÇ E‚ÇÇ),
  trivialization_at := Œª b, (trivialization_at F‚ÇÅ E‚ÇÅ b).prod (trivialization_at F‚ÇÇ E‚ÇÇ b),
  mem_base_set_trivialization_at :=
    Œª b, ‚ü®mem_base_set_trivialization_at F‚ÇÅ E‚ÇÅ b, mem_base_set_trivialization_at F‚ÇÇ E‚ÇÇ b‚ü©,
  trivialization_mem_atlas := Œª b,
    ‚ü®(_, _), ‚ü®trivialization_mem_atlas F‚ÇÅ E‚ÇÅ b, trivialization_mem_atlas F‚ÇÇ E‚ÇÇ b‚ü©, rfl‚ü©}
